buildscript {
    ext {
        springBootVersion = "1.2.5.RELEASE"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
    }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE")
        classpath "com.moowork.gradle:gradle-node-plugin:0.10"
        classpath 'org.springframework:springloaded:1.2.3.RELEASE'
    }
}
    

plugins {
  id "com.moowork.node" version "0.10"
}

apply plugin: 'java'
apply plugin: 'spring-boot'

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"

    compile 'org.springframework.cloud:spring-cloud-starter-zuul:1.0.3.RELEASE'


    
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

node {
    version = '0.12.2'
    npmVersion = '2.7.5'
    download = true
}


task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}
task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmPackages
}

task bowerSync(type: Sync) {
    from 'bower_components'
    into 'src/main/resources/public/bower_components'
    dependsOn bowerInstall
}

clean.delete << file('bower_components')